/*
 * Copyright (c) 2015-2017 Uli Bubenheimer
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.0-beta01'
    }
}

repositories {
    google()
    jcenter()
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = 'org.bubenheimer'
version = '1.0-SNAPSHOT'

String supportLibVersion = '28.0.0-alpha1'

publishing {
    publications {
        library(MavenPublication) {
            artifact bundleReleaseAar

            pom.withXml {
                def deps = asNode().appendNode('dependencies')

                // Give users access to RecyclerView.ViewHolder
                def recyclerview = deps.appendNode('dependency')
                recyclerview.appendNode('groupId', 'com.android.support')
                recyclerview.appendNode('artifactId', 'recyclerview-v7')
                recyclerview.appendNode('version', supportLibVersion)
            }
        }
    }
}

android {
    compileSdkVersion 28

    defaultConfig {
        targetSdkVersion 28
        minSdkVersion 14
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
        debug {
        }
        release {
            shrinkResources false
            minifyEnabled false
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
        }
    }
    compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_8
      targetCompatibility JavaVersion.VERSION_1_8
    }

    // TODO change based on resolution of https://issuetracker.google.com/issues/72050365
    libraryVariants.all {
        it.generateBuildConfig.enabled = false
    }

    dataBinding {
        enabled = true
    }

    dependencies {
        implementation "com.android.support:recyclerview-v7:$supportLibVersion"
        implementation "com.android.support:design:$supportLibVersion"
    }
}
