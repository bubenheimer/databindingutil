/*
 * Copyright (c) 2015-2020 Uli Bubenheimer
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

buildscript {
    ext.kotlin_version = '1.4.0'

    repositories {
        google()
        gradlePluginPortal()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.0-rc02'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    google()
    jcenter()
    mavenLocal()
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

group = 'org.bubenheimer'
version = '1.0-SNAPSHOT'

android {
    compileSdkVersion 30

    defaultConfig {
        targetSdkVersion 30
        minSdkVersion 14
        versionCode 1
        versionName '1.0'
    }
    buildTypes {
        debug {
        }
        release {
            shrinkResources false
            minifyEnabled false
            debuggable false
            jniDebuggable false
            renderscriptDebuggable false
        }
    }

    kotlinOptions {
        jvmTarget = "1.6"
        freeCompilerArgs = [
                "-progressive",
                "-Xexplicit-api=strict"
        ]
    }

    buildFeatures {
        buildConfig false
        dataBinding true
    }

    dependencies {
        debugImplementation 'org.bubenheimer:androidutil-debug:1.0-SNAPSHOT'
        releaseImplementation 'org.bubenheimer:androidutil-release:1.0-SNAPSHOT'

        implementation 'org.bubenheimer:javautil:1.0-SNAPSHOT'

        // api to give users access to RecyclerView.ViewHolder
        api 'androidx.recyclerview:recyclerview:1.1.0'
        implementation 'com.google.android.material:material:1.2.1'

        implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
        implementation 'androidx.viewpager2:viewpager2:1.0.0'

        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    }

    task debugSourceJar(type: Jar) {
        sourceSets {
            from debug.java.srcDirs, main.java.srcDirs
        }
        //noinspection GroovyAccessibility
        archiveClassifier = "sources"
    }
}

publishing {
    publications {
        debug(MavenPublication) {
            afterEvaluate {
                from components.debug
                artifactId = "$project.name-debug"

                artifact(debugSourceJar)
            }
        }

        release(MavenPublication) {
            afterEvaluate {
                from components.release
                artifactId = "$project.name-release"
            }
        }
    }
}
